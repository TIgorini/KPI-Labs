*********************
* .MEASURE LIBRARY  *
*********************

* PULSE WIDTH OF WAVEFORM EXPRESSION X
* EXAMPLE: PW(V(OUT1))
.BEGIN_MEASURE PW(X) AUTO_ADD
.MEASURE TRAN AVG1 AVG X
.MEASURE TRAN T1 TARG X VAL=AVG1 RISE=1
.MEASURE TRAN T2 TARG X VAL=AVG1 FALL=1
.MEASURE TRAN PW PARAM T2-T1
.END_MEASURE PW

* OVERSHOOT OF WAVEFORM EXPRESSION X
* EXAMPLE: OVERSHOOT(V(OUT1))
.BEGIN_MEASURE OVERSHOOT(X,TLIMIT)
.MEASURE TRAN AVG1 AVG X
.MEASURE TRAN PK1 MAX X FROM=0 TO=TLIMIT
.MEASURE TRAN T1 TARG X VAL=AVG1 RISE=1
.MEASURE TRAN T2 TARG X VAL=AVG1 FALL=1
.MEASURE TRAN BASE FIND X AT=T1+.8*(T2-T1)
.MEASURE TRAN OVERSHOOT PARAM PK1-BASE
.END_MEASURE OVERSHOOT

* PULSE FREQUENCY OF WAVEFORM EXPRESSION X
* EXAMPLE: PULSE_FREQ(V(OUT1)) 
.BEGIN_MEASURE PULSE_FREQ(X) AUTO_ADD
.MEASURE TRAN AVG1 AVG X
.MEASURE TRAN T1 TARG X VAL=AVG1 RISE=1
.MEASURE TRAN T2 TARG X VAL=AVG1 RISE=2
.MEASURE TRAN TP PARAM T2-T1
.MEASURE TRAN PULSE_FREQ PARAM 1/TP
.END_MEASURE PULSE_FREQ

* PULSE PERIOD OF WAVEFORM EXPRESSION X
* EXAMPLE: PULSE_PERIOD(V(OUT1))
.BEGIN_MEASURE PULSE_PERIOD(X) AUTO_ADD
.MEASURE TRAN AVG1 AVG X
.MEASURE TRAN T1 TARG X VAL=AVG1 RISE=1
.MEASURE TRAN T2 TARG X VAL=AVG1 RISE=2
.MEASURE TRAN PULSE_PERIOD PARAM T2-T1
.END_MEASURE PULSE_PERIOD

* DELAY FROM WAVEFORM EXPRESSION X TO WAVEFROM EXPRESSION Y
* EXAMPLE: PULSE_DELAY(V(OUT1),V(OUT2))
.BEGIN_MEASURE PULSE_DELAY(X,Y)
.MEASURE TRAN AVG1 AVG X
.MEASURE TRAN AVG2 AVG X
.MEASURE TRAN T1 TARG X VAL=AVG1 RISE=1
.MEASURE TRAN T2 TARG Y VAL=AVG2 RISE=1
.MEASURE TRAN PULSE_DELAY PARAM T2-T1
.END_MEASURE PULSE_DELAY

* POWER FACTOR CALCULATION USING THE INPUT SOURCE SRC
* EXAMPLE: POWER_FACTOR(V1,100m)
.BEGIN_MEASURE POWER_FACTOR(SRC,TSTART=0)
.MEASURE TRAN PappI RMS -I(SRC) FROM=TSTART
.MEASURE TRAN PappV RMS V(SRC) FROM=TSTART
.MEASURE TRAN Papp PARAM=PappV*PappI
.MEASURE TRAN Preal AVG V(SRC)*(-I(SRC)) FROM=TSTART
.MEASURE TRAN POWER_FACTOR PARAM=Preal/Papp
.END_MEASURE POWER_FACTOR
;$SpiceType=AMBIGUOUS
